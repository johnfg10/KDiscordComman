def aVersion = "1.0.1s"
def aGroup = "io.github.johnfg10"
def aDevName = "johnfg10"
def aDevEmail = "j.gray26@hotmail.com"

version aVersion
group aGroup

repositories {
    maven {
        url  "https://jitpack.io"
    }
    maven {
        url "https://dl.bintray.com/johnfg10/KDiscordCommandFramework"
    }
}

dependencies {

    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.2.21'
    compile 'com.google.code.gson:gson:2.8.2'
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://github.com/johnfg10/KDiscordCommand/blob/master/LICENCE.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id aDevName
            name aDevName
            email aDevEmail
        }
    }
    scm {
        url "https://github.com/johnfg10/KDiscordCommand"
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MainPub(MavenPublication) {
            from components.java
            groupId aGroup
            artifactId 'KDiscordCommandCore'
            version aVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'makes commands on discord a breeze')
                root.appendNode('name', 'KDiscordCommandCore')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publish = true

    publications = ['MainPub']

    pkg {
        repo = 'KDiscordCommandFramework'
        name = 'KDiscordCommandCore'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/johnfg10/KDiscordCommand.git'
        version {
            name = aVersion
            desc = 'makes commands on discord a breeze'
            vcsTag = aVersion
        }
    }
}
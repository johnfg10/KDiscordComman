def aVersion = "1.0.3"
def aGroup = "io.github.johnfg10"
def aDevName = "johnfg10"
def aDevEmail = "j.gray26@hotmail.com"

version aVersion
group aGroup

repositories {
    maven {
        url  "https://jitpack.io"
    }
    maven {
        url "https://dl.bintray.com/johnfg10/KDiscordCommandFramework"
    }
}

dependencies {
    //compile 'io.github.johnfg10:KDiscordCommandCore:1.0.0'
    compile project(":Core")
    compile "com.github.austinv11:Discord4J:2.9.3"
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://github.com/johnfg10/KDiscordsCommand/blob/master/LICENCE.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id aDevName
            name aDevName
            email aDevEmail
        }
    }
    scm {
        url "https://github.com/johnfg10/KDiscordCommand"
    }
}

jar {
}

// Create the publication with the pom configuration:
publishing {
    publications {
        java(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            groupId "io.github.johnfg10"
            artifactId 'KDiscordCommandDiscord4j'
            version aVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'makes commands on discord a breeze')
                root.appendNode('name', 'KDiscordCommandDiscord4j')
                def node = root.appendNode('repositories')
                node.appendNode('repository').appendNode('url', 'https://jcenter.bintray.com')
                node.appendNode('repository').appendNode('url', 'https://jitpack.io')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publish = true

    publications = ['java']

    pkg {
        repo = 'KDiscordCommandFramework'
        name = 'KDiscordCommandDiscord4j'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/johnfg10/KDiscordCommand.git'
        version {
            name = aVersion
            desc = 'makes commands on discord a breeze'
            vcsTag = aVersion
        }
    }
}